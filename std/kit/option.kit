/**
 * Represents a nullable value.
 */
enum Option[T] {
    Some(value: T);
    None;

    public function isSome(): Bool {
        match this {
            None => return false;
            default => return true;
        }
    }
    public function isNone(): Bool {
        match this {
            None => return true;
            default => return false;
        }
    }

    rules {
        (this.isSome()) => !this.isNone();

        (this.unwrap()) => match this {
            Some(v) => v;
            default => throw NoValueError;
        }

        (this ?? (other: T)) => match this {
            Some(v) => v;
            default => other;
        }

        (this ?. $id) => match this {
            Some(v) => Some(v.$id);
            default => None;
        }
    }
}
